plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '160212'

repositories {
	maven {
		url "http://dl.bintray.com/ihmcrobotics/maven-release"
	}

	mavenCentral()
}

dependencies {
	compile group: 'net.java.jutils', name: 'jutils', version: '1.0.0'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
	compile group: 'us.ihmc', name: 'IHMCNativeLibraryLoader', version: '1.0'
	runtime group: 'net.java.jinput', name: 'jinput-platform', version: '2.0.6', classifier: 'natives-windows'
	runtime group: 'net.java.jinput', name: 'jinput-platform', version: '2.0.6', classifier: 'natives-osx'
	runtime group: 'net.java.jinput', name: 'jinput-platform', version: '2.0.6', classifier: 'natives-linux'
}

sourceSets {
    main {
        java {
            srcDirs = ['applet/src',
                       'coreAPI/src/java',
                       'plugins/awt/src',
                       'plugins/linux/src/java',
                       'plugins/OSX/src/java',
                       'plugins/windows/src/java',
                       'plugins/wintab/src/java']
        }
    }
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "Java Game Initiative")
   }
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc.thirdparty.jinput'
			artifactId 'jinput'
			version "$version"
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}

	}
}

bintray {
    user = project.hasProperty("bintray_user") ? project.bintray_user : "invalid"
    key = project.hasProperty("bintray_key") ? project.bintray_key : "invalid"

    if (user.equals("invalid")) {
        project.logger.debug("Bintray user name property not set. Please set the 'bintray_user' property in ~/.gradle/gradle.properties. See https://github.com/bintray/gradle-bintray-plugin")
    }

    if (key.equals("invalid")) {
        project.logger.debug("Bintray API key property not set. Please set the 'bintray_key' property in ~/.gradle/gradle.properties. See https://github.com/bintray/gradle-bintray-plugin")
    }

    dryRun = false
    publish = false

    publications = ["mavenJava"]

    pkg {
        repo = "maven-vendor"
        userOrg = "ihmcrobotics"
        name = "IHMCJinputMultiArch"
        desc = "Maven repository providing a multi-arch JInput artifact"
				vcsUrl = "https://github.com/ihmcrobotics/ihmc-jinput-multi-arch.git"

        licenses = ['BSD 2-Clause']
        labels = ['java', 'jinput', 'vendored']

        version {
            name = project.version
            released = new Date()
        }
    }
}
